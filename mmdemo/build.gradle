plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "cloud.bjx.mm.android"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 7
        versionName "0.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    signingConfigs {
        release {
            storeFile file('./mmdemo-key.jks')
            storePassword 'runx2020'
            keyAlias 'mmdemo'
            keyPassword 'runx2020'
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                renameAPK(variant)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }

    buildFeatures {
        viewBinding true
        dataBinding = true
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation project(':faceunity')

    // Testing dependencies
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'

    // Lifecycle, LiveData, ViewModel
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'

    // Kotlin and Coroutines
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    // BaseRecyclerViewAdapterHelper
    implementation('com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }

    // okhttp3
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okio:okio:2.9.0'

    // Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // flex box layout
    implementation 'com.google.android:flexbox:2.0.1'

    // BugLy SDK
    implementation 'com.tencent.bugly:crashreport:3.3.3'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
}

def renameAPK(variant) {
    def buildType = variant.buildType.name
    variant.outputs.all {
        outputFileName = "MMRTC-" + buildType + ".apk"
    }
}